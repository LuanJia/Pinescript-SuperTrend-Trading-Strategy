//@version=4
strategy("SuperTrend Strategy", overlay=true, default_qty_value=5, currency=currency.USD, default_qty_type=strategy.percent_of_equity, initial_capital=10000, max_bars_back=4000)

//===== Timeframe ==========================================================================//

FromMonth = input(defval = 1, title = "From Month", minval = 1, maxval = 12)
FromDay = input(defval = 1, title = "From Day", minval = 1, maxval = 31)
FromYear = input(defval = 2020, title = "From Year", minval = 2017)
ToMonth = input(defval = 1, title = "To Month", minval = 1, maxval = 12)
ToDay = input(defval = 1, title = "To Day", minval = 1, maxval = 31)
ToYear = input(defval = 9999, title = "To Year", minval = 2017)
start = timestamp(FromYear, FromMonth, FromDay, 00, 00)
finish = timestamp(ToYear, ToMonth, ToDay, 23, 59)
window() => time >= start and time <= finish ? true : false

//===== INPUTS ==========================================================================//

factor = input(3, title="ATR Factor")
period = input(10, minval = 1, maxval = 100, title="ATR Period")
up = (high + low) / 2 - (factor * atr(period))
down = (high + low) / 2 + (factor * atr(period))
trend_up = 0.0
trend_up := close[1] > trend_up[1] ? max(up, trend_up[1]) : up
trend_down = 0.0
trend_down := close[1] < trend_down[1] ? min(down, trend_down[1]) : down
trend = 0.0
trend := close > trend_down[1] ? 1 : close < trend_up[1] ? -1 : nz(trend[1], 1)
tsl = trend == 1 ? trend_up : trend_down
line_color = trend == 1 ? "green" : "red"
long_signal = trend == 1 and trend[1] == -1
short_signal = trend == -1 and trend[1] == 1
background = input(title = "Background On/Off ?", type = input.bool, defval = true)

//===== PLOTS ==========================================================================//

// Line
line_plot = plot(tsl, color = trend == 1 ? color.green : color.red, linewidth = 2, title = "Trend Line")
// Labels
plotshape(long_signal ? up : na, title = "Buy", text = "Buy", location = location.absolute, style = shape.labelup, size = size.tiny, color = color.green, textcolor = color.white, transp = 0)
plotshape(short_signal ? down : na, title = "Sell", text = "Sell", location = location.absolute, style = shape.labeldown, size = size.tiny, color = color.red, textcolor = color.white, transp = 0)
// Circles
plotshape(long_signal ? up : na, title = "Uptrend starts", location = location.absolute, style = shape.circle, size = size.tiny, color = color.green, transp = 0)
plotshape(short_signal ? down : na, title = "Downtrend starts", location = location.absolute, style = shape.circle, size = size.tiny, color = color.red, transp = 0)
// Background
long_fill = background ? (trend == 1 ? color.green : na) : na
short_fill = background ? (trend == -1 ? color.red : na) : na
candle_plot = plot(ohlc4, title = "Price Line", color = trend == 1 ? long_fill : short_fill, transp = 90, linewidth = 2)
fill(candle_plot, line_plot, title = "Long Background", color = long_fill)
fill(candle_plot, line_plot, title = "Short Background", color = short_fill)

//===== STRATEGY ==========================================================================//
if (long_signal and window())
    strategy.entry("long", strategy.long)
if (short_signal and window())
    strategy.entry("short", strategy.short)
